package core.map

use core
use core.hash
use core.memory
use core.math
use core.conv

use core {Optional}
use core.intrinsics.onyx { __initialize }

/// Map is a generic hash-map implementation that uses chaining.
/// Values can be of any type. Keys must of a type that supports
/// the core.hash.hash, and the '==' operator.
@conv.Custom_Format.{ #solidify format_map {K=Key_Type, V=Value_Type} }
Map :: struct (Key_Type: type_expr, Value_Type: type_expr) where ValidKey(Key_Type) {
    allocator : Allocator;

    hashes  : [] i32;
    entries : [..] Entry(Key_Type, Value_Type);

    Entry :: struct (K: type_expr, V: type_expr) {
        next  : i32;
        hash  : u32;
        key   : K;
        value : V;
    }
}

#local ValidKey :: interface (T: type_expr) {
    // In order to use a certain type as a key in a Map, you must
    // provide an implementation of core.hash.hash() for that type,
    // and you must provide an operator overload for ==.

    t as T;

    { hash.hash(t) } -> u32;
    { t == t       } -> bool;
}


builtin.Map :: Map


/// Allows for creation of a Map using make().
///
///     m := make(Map(str, i32));
#overload
__make_overload :: macro (x: &Map($K, $V), allocator := context.allocator) =>
    #this_package.Map.make(K, V, allocator);

/// Creates and initializes a new map using the types provided.
Map.make :: macro ($Key: type_expr, $Value: type_expr, allocator := context.allocator) -> Map(Key, Value) {
    map : Map(Key, Value);
    #this_package.Map.init(&map, allocator);
    return map;
}

/// Initializes a map.
Map.init :: (map: &Map($K, $V), allocator := context.allocator) {
    __initialize(map);

    map.allocator = allocator;

    map.hashes = builtin.make([] u32, 8, allocator=allocator);
    Array.fill(map.hashes, -1);

    Array.init(&map.entries, allocator=allocator);
}

// Allows for deletion of a Map using `delete(&map)`.
#overload
builtin.delete :: Map.free

/// Destroys a map and frees all memory.
Map.free :: (use map: &Map) {
    if hashes.data != null  do Slice.free(&hashes, allocator=allocator);
    if entries.data != null do Array.free(&entries);
}

/// Shallow copies a map using the allocator provided if one is provided, or the allocator on the old map otherwise.
Map.copy :: #match #local {}

#overload
Map.copy :: (oldMap: &Map, allocator: ? Allocator = .None) -> Map(oldMap.Key_Type, oldMap.Value_Type) {
    newMap: typeof *oldMap;
    newMap.allocator = allocator ?? oldMap.allocator;
    newMap.hashes = Array.copy(oldMap.hashes, newMap.allocator);
    newMap.entries = Array.copy(&oldMap.entries, newMap.allocator);

    return newMap;
}

#overload
Map.copy :: (oldMap: Map, allocator: ? Allocator = .None) -> Map(oldMap.Key_Type, oldMap.Value_Type) {
    newMap: typeof oldMap
    newMap.allocator = allocator ?? oldMap.allocator
    newMap.hashes = Array.copy(oldMap.hashes, newMap.allocator)
    newMap.entries = Array.copy(&oldMap.entries, newMap.allocator)

    return newMap
}

/// Sets the value at the specified key, or creates a new entry
/// if the key was not already present.
Map.put :: (use map: &Map, key: map.Key_Type, value: map.Value_Type) {
    lr := lookup(map, key);

    if lr.entry_index >= 0 {
        entries[lr.entry_index].value = value;
        return;
    }

    entries << .{ hashes[lr.hash_index], lr.hash, key, value };
    hashes[lr.hash_index] = entries.count - 1;

    if full(map) do grow(map);
}

/// Returns true if the map contains the key.
Map.has :: (use map: &Map, key: map.Key_Type) -> bool {
    lr := lookup(map, key);
    return lr.entry_index >= 0;
}

/// Returns the value at the specified key, or `.None` if the value
/// is not present
Map.get :: (use map: &Map, key: map.Key_Type) -> ? map.Value_Type {
    lr := lookup(map, key);
    if lr.entry_index >= 0 do return entries[lr.entry_index].value;

    return .{};
}

/// Returns a pointer to the value at the specified key, or null if
/// the key is not present.
Map.get_ptr :: (use map: &Map, key: map.Key_Type) -> &map.Value_Type {
    lr := lookup(map, key);
    if lr.entry_index >= 0 do return &entries[lr.entry_index].value;

    return null;
}

/// Returns a pointer to the value at the specified key. If the key
/// is not in the map, a new value is created and inserted, then the
/// pointer to that value is returned.
Map.get_ptr_or_create :: (use map: &Map, key: map.Key_Type) -> &map.Value_Type {
    lr := lookup(map, key);
    if lr.entry_index < 0 {
        put(map, key, .{});
        lr = lookup(map, key);
    }

    return &entries[lr.entry_index].value;
}

/// **DEPRECATED** - Use `map.get` instead.
///
/// Returns an Optional of the value at the specified key. The Optional
/// has a value if the key is present, otherwise the optional does not
/// have a value.
Map.get_opt :: (use map: &Map, key: map.Key_Type) -> ?map.Value_Type {
    lr := lookup(map, key);
    if lr.entry_index >= 0 do return Optional.make(entries[lr.entry_index].value);

    return .{};
}

/// Removes an entry from the map.
Map.delete :: (use map: &Map, key: map.Key_Type) {
    lr := lookup(map, key);
    if lr.entry_index < 0 do return;

    if lr.entry_prev < 0   do hashes[lr.hash_index]       = entries[lr.entry_index].next;
    else                   do entries[lr.entry_prev].next = entries[lr.entry_index].next;

    if lr.entry_index == entries.count - 1 {
        Array.pop(&entries);
        return;
    }

    Array.fast_delete(&entries, lr.entry_index);
    last := lookup(map, entries[lr.entry_index].key);

    if last.entry_prev >= 0    do entries[last.entry_prev].next = lr.entry_index;
    else                       do hashes[last.hash_index] = lr.entry_index;
}

/// Helper macro that finds a value by the key, and if it exists,
/// runs the code, providing an `it` variable that is a pointer
/// to the value.
/// 
///     m: Map(str, i32);
///     m->update("test") {
///         *it += 10;
///     }
/// or:
///     m->update("test", [v](*v += 10));
Map.update :: macro (map: ^Map, key: map.Key_Type, body: Code) {
    lookup_ :: lookup
    lr := lookup_(map, key);

    if lr.entry_index >= 0 {
        it := &map.entries[lr.entry_index].value;
        #unquote body(it);
    }
}

/// Removes all entries from the hash map. Does NOT
/// modify memory, so be wary of dangling pointers!
Map.clear :: (use map: &Map) {
    for i in 0 .. hashes.count do hashes.data[i] = -1;
    entries.count = 0;
}

/// Returns if the map does not contain any elements.
Map.empty :: (use map: &Map) -> bool {
    return entries.count == 0;
}

/// Helper procedure to nicely format a Map when printing.
/// Rarely ever called directly, instead used by conv.format_any.
Map.format_map :: (output: &conv.Format_Output, format: &conv.Format, x: &Map($K, $V)) {
    if format.pretty_printing {
        output->write("{\n");
        for& x.entries {
            conv.format(output, "    {\"p} => {\"p}\n", it.key, it.value);
        }
        output->write("}");

    } else {
        output->write("{ ");
        for& x.entries {
            if !#first do output->write(", ");
            conv.format(output, "{\"p} => {\"p}", it.key, it.value);
        }
        output->write(" }");
    }
}

/// Quickly create a Map with some entries.
///
///     Map.literal(str, i32, .[
///         .{ "test", 123 },
///         .{ "foo",  456 },
///     ]);
Map.literal :: ($Key: type_expr, $Value: type_expr, values: [] MapLiteralValue(Key, Value)) => {
    m := core.map.make(Key, Value);
    for & values {
        m->put(it.key, it.value);
    }

    return m;
}

#local
MapLiteralValue :: struct (K: type_expr, V: type_expr) {
    key: K;
    value: V;
}

/// Produces an iterator that yields all values of the map,
/// in an unspecified order, as Map is unordered.
Map.as_iter :: (m: &Map) =>
    core.iter.generator(
        &.{ m = m, i = 0 },

        ctx => {
            if ctx.i < ctx.m.entries.count {
                defer ctx.i += 1;
                return Optional.make(&ctx.m.entries.data[ctx.i]);
            }

            return .None;
        });


/// Allows for looping over a map with a for-loop
#overload
__for_expansion :: macro (map: Map($K, $V), $flags: __For_Expansion_Flags, $body: Code) where (body.capture_count == 2) {
    m        := map
    m_data   := m.entries.data
    m_length := m.entries.length
    i := 0
    while i < m_length {
        defer i += 1

        #if flags & .BY_POINTER {
            #unquote body(m_data[i].key, &m_data[i].value) #skip_scope(2)
        } else {
            #unquote body(m_data[i].key, m_data[i].value) #skip_scope(2)
        }
    }
}

#overload
__for_expansion :: macro (map: &Map($K, $V), $flags: __For_Expansion_Flags, $body: Code) where (body.capture_count == 2) {
    m := map
    i := 0
    while i < m.entries.length {
        defer i += 1

        #if flags & .BY_POINTER {
            #unquote body(m.entries[i].key, &m.entries[i].value) #skip_scope(2)
        } else {
            #unquote body(m.entries[i].key, m.entries[i].value) #skip_scope(2)
        }
    }
}


//
// Helper operator overloads for accessing values, accessing
// values by pointer, and setting values.
#operator []  macro (map: Map($K, $V), key: K) -> ?V     { return #this_package.Map.get(&map, key); }
#operator &[] macro (map: Map($K, $V), key: K) -> &V     { return #this_package.Map.get_ptr(&map, key); }
#operator []= macro (map: Map($K, $V), key: K, value: V) { #this_package.Map.put(&map, key, value); }

//
// Private symbols
// 
// These are used for the implementation of Map,
// but do not need to be used by any other part
// of the code.
//

#local {
    MapLookupResult :: struct {
        hash_index  : i32 = -1;
        entry_index : i32 = -1;
        entry_prev  : i32 = -1;
        hash        : u32 = 0;
    }

    lookup :: (use map: &Map, key: map.Key_Type) -> MapLookupResult {
        if hashes.data == null do init(map);
        lr := MapLookupResult.{};

        hash_value: u32 = hash.hash(key);
        lr.hash = hash_value;

        lr.hash_index = hash_value % hashes.count;
        lr.entry_index = hashes[lr.hash_index];

        while lr.entry_index >= 0 {
            if entries[lr.entry_index].hash == hash_value {
                if entries[lr.entry_index].key == key do return lr;
            }

            lr.entry_prev = lr.entry_index;
            lr.entry_index = entries[lr.entry_index].next;
        }

        return lr;
    }

    full :: (use map: &Map) => entries.count >= (hashes.count >> 2) * 3;

    grow :: (use map: &Map) {
        new_size := math.max(hashes.count << 1, 8);
        rehash(map, new_size);
    }

    rehash :: (use map: &Map, new_size: i32) {
        memory.free_slice(&hashes, allocator);
        hashes = builtin.make([] u32, new_size, allocator=allocator);
        Array.fill(hashes, -1);

        for &entry, index in entries {
            hash_index := entry.hash % hashes.count;
            entries[index].next = hashes[hash_index];
            hashes[hash_index] = index;
        }
    }
}

//
// Everything below here only exists for backwards compatibility.
//

make :: Map.make
init :: Map.init
free :: Map.free
copy :: Map.copy
has :: Map.has
get :: Map.get
get_ptr :: Map.get_ptr
get_opt :: Map.get_opt
get_ptr_or_create :: Map.get_ptr_or_create
put :: Map.put
delete :: Map.delete
update :: Map.update
clear :: Map.clear
empty :: Map.empty
literal :: Map.literal
as_iter :: Map.as_iter


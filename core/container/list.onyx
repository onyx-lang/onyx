package core.list

use package core.intrinsics.onyx { __zero_value }

ListElem :: struct (T: type_expr) {
    next: ^ListElem(T) = null;
    prev: ^ListElem(T) = null;
    data: T;
}

List :: struct (T: type_expr) {
    allocator: Allocator;

    first: ^ListElem(T) = null;
    last:  ^ListElem(T) = null;
}

make :: ($T: type_expr, allocator := context.allocator) -> List(T) {
    return .{ allocator = allocator };
}

push_end :: (list: ^List($T), x: T) { 
    new_elem := allocate_elem(list);
    new_elem.data = x;

    new_elem.prev = list.last;
    list.last.next = new_elem;
    list.last = new_elem;

    if list.first == null do list.first = new_elem;
}

push_begin :: (list: ^List($T), x: T) {
    new_elem := allocate_elem(list);
    new_elem.data = x;

    new_elem.next = list.first;
    list.first.prev = new_elem;
    list.first = new_elem;

    if list.last == null do list.last = new_elem;
}

pop_end :: (list: ^List($T), default: T = __zero_value(T)) -> T {
    if list.last == null do return default;

    end := list.last;
    list.last = list.last.prev;
    list.last.next = null;

    defer raw_free(list.allocator, end);
    return end.data;
}

pop_begin :: (list: ^List($T), default: T = __zero_value(T)) -> T {
    if list.last == null do return default;

    begin := list.first;
    list.first = list.first.next;
    list.first.prev = null;

    defer raw_free(list.allocator, begin);
    return begin.data;
}

contains :: (list: ^List($T), x: T) -> bool {
    elem := list.first;
    while elem != null {
        if elem.data == x do return true;
        elem = elem.next;
    }

    return false;
}

fold :: (list: ^List($T), init: $R, f: (T, R) -> R) -> R {
    val := init;

    link := list.first;
    while link != null {
        val = f(link.data, val);
        link = link.next;
    }

    return val;
}

// map :: #match {
//     (list: ^List($T), f: (^T) -> void) {
//     }
// }

get_iterator :: (list: ^List($T)) -> Iterator(T) {
    iterator_next :: ($T: type_expr, data: rawptr) -> (T, bool) {
        list_iter := cast(^ListIterator(T)) data;

        use package core.intrinsics.onyx { __zero_value }
        if list_iter.current == null do return __zero_value(T), false;

        defer list_iter.current = list_iter.current.next;
        return list_iter.current.data, true;
    }

    iterator_close :: (data: rawptr) {
        cfree(data);
    }

    ListIterator :: struct (T: type_expr) {
        current: ^ListElem(T);
    }

    list_iterator := new(ListIterator(T));
    list_iterator.current = list.first;

    return .{
        data = list_iterator,
        next = #solidify iterator_next { T = T },
        close = iterator_close,
    };
}

#local
allocate_elem :: macro (list: ^List($T)) => new(ListElem(T), allocator=list.allocator);

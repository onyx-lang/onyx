package core.slice

use core.intrinsics.types {type_is_struct}
use core.memory

//
// [] $T == Slice(T)
//   where
// Slice :: struct (T: type_expr) {
//     data: &T;
//     count: u32;
// }
//

#doc """
    Creates a zeroed slice of type `T` and length `length`, using the allocator provided.

        use core {slice, println}

        sl := slice.make(i32, 10);
        println(sl);
        // 0 0 0 0 0 0 0 0 0 0
"""
make :: ($T: type_expr, length: u32, allocator := context.allocator) -> [] T {
    data := raw_alloc(allocator, sizeof T * length);
    memory.set(data, 0, sizeof T * length);
    return .{ data, length };
}

#doc """
    Initializes a slice of type `T` to have a length of `length`, using the allocator provided.

        use core {slice, println}

        sl: [] i32;
        slice.init(&sl, 10);
        println(sl);
        // 0 0 0 0 0 0 0 0 0 0
"""
init :: (sl: &[] $T, length: u32, allocator := context.allocator) -> [] T {
    sl.count = length;
    sl.data = raw_alloc(allocator, sizeof T * length);
    memory.set(sl.data, 0, sizeof T * length);
}

#doc """
    Frees the data inside the slice.
    The slice is taken by pointer because this procedure also sets the data pointer to `null`, and the length to `0`,
    to prevent accidental future use of the slice.
"""
free :: (sl: &[] $T, allocator := context.allocator) {
    if sl.data == null do return;

    raw_free(allocator, sl.data);
    sl.data = null;
    sl.count = 0;
}

//
// Allows for make([] i32).
#overload
__make_overload :: macro (_: &[] $T, count: u32, allocator := context.allocator) -> [] T {
    use core.memory

    ret := #this_package.make(T, count, allocator);
    memory.set(ret.data, 0, sizeof T * count);
    return ret;
}

//
// Allows for delete(&sl);
#overload
builtin.delete :: macro (x: &[] $T, allocator := context.allocator) {
    #this_package.free(x, allocator);
}


#doc """
    Copies a slice to a new slice, allocated from the provided allocator.
"""
copy :: (sl: [] $T, allocator := context.allocator) -> [] T {
    data := raw_alloc(allocator, sl.count * sizeof T);
    memory.copy(data, sl.data, sl.count * sizeof T);

    return .{ data = data, count = sl.count };
}


#doc """
    Moves an element to a new index, ensuring that order of other elements is retained.

        use core {slice, println}

        arr := i32.[1, 2, 3, 4, 5, 6, 7, 8, 9, 10];

        // Move element at index 4 to index 8
        slice.transplant(arr, 4, 8);

        println(arr);
        // 1 2 3 4 6 7 8 9 5 10
"""
transplant :: (arr: [] $T, old_index: i32, new_index: i32) -> bool {
    if old_index < 0 || old_index >= arr.count do return false;
    if new_index < 0 || new_index >= arr.count do return false;
    if old_index == new_index do return true;

    value := arr.data[old_index];

    if old_index < new_index { // Moving forward
        while i := old_index; i < new_index {
            defer i += 1;
            arr.data[i] = arr.data[i + 1];
        }

    } else { // Moving backward
        while i := old_index; i > new_index {
            defer i -= 1;
            arr.data[i] = arr.data[i - 1];
        }
    }

    arr.data[new_index] = value;
    return true;
}

#doc """
    Get an element from a slice, with negative and wrap-around indexing.
"""
get :: (arr: [] $T, idx: i32) -> T {
    if arr.count == 0 do return .{};

    while idx < 0          do idx += arr.count;
    while idx >= arr.count do idx -= arr.count;

    return arr.data[idx];
}

#doc """
    Get a pointer to an element from a slice, with negative and wrap-around indexing.
"""
get_ptr :: (arr: [] $T, idx: i32) -> &T {
    if arr.count == 0 do return null;

    while idx < 0          do idx += arr.count;
    while idx >= arr.count do idx -= arr.count;

    return &arr.data[idx];
}

#doc """
    Set a value in a slice, with negative and wrap-around indexing.
"""
set :: (arr: [] $T, idx: i32, value: T) {
    if arr.count == 0 do return;

    while idx < 0          do idx += arr.count;
    while idx >= arr.count do idx -= arr.count;

    arr.data[idx] = value;
}

contains :: #match #locked {
    (arr: [] $T, x: T) -> bool {
        for it: arr do if it == x do return true;
        return false;
    }, 

    macro (arr: [] $T, $cmp: Code) -> bool {
        for it: arr do if #unquote cmp do return true;
        return false;
    }
}

#doc """
    Tests if the slice is empty.

    Normally this is unneeded, as arrays have a 'truthiness'
    that depends on their count. For example, instead of saying:

        if slice.empty(arr) { ... }

    You can simply say:

        if !arr { ... }
"""
empty :: (arr: [] $T) => arr.count == 0;

#doc "Uses `+` to sum all elements in the slice."
sum :: (arr: [] $T, start: T = 0) -> T {
    sum := start;
    for it: arr do sum += it;
    return sum;
}

#doc "Uses `*` to multiply all elements in the slice."
product :: (arr: [] $T, start: T = 1) -> T {
    prod := start;
    for it: arr do prod *= it;
    return prod;
}

#doc """
    Uses `+` to add the elements together.
    Then use `/ i32` to divide by the number of elements.
    Both of these are assumed to work.
"""
average :: (arr: [] $T) -> T {
    sum := cast(T) 0;
    for it: *arr do sum += it;

    return sum / cast(T) arr.count;
}

#doc """
    Reverses a slice in-place.
"""
reverse :: (arr: [] $T) {
    for i: arr.count / 2 {
        tmp := arr[i];
        arr[i] = arr[arr.count - 1 - i];
        arr[arr.count - 1 - i] = tmp;
    }
}

#doc """
    Simple insertion sort.
    
    `cmp` should return greater-than 0 if `left > right`.

    Returns the array to be used in '|>' chaining.

    **Not a copy of the slice.**
"""
sort :: #match #local {}

#overload
sort :: (arr: [] $T, cmp: (T, T) -> i32) -> [] T {
    for i: 1 .. arr.count {
        x := arr.data[i];
        j := i - 1;

        // @ShortCircuitLogicalOps
        // This is written this way because '&&' does not short circuit right now.
        while j >= 0 {
            if cmp(arr.data[j], x) > 0 {
                arr.data[j + 1] = arr.data[j];
                j -= 1;
            } else {
                break;
            }
        }

        arr.data[j + 1] = x;
    }

    return arr;
}

#overload
sort :: (arr: [] $T, cmp: (&T, &T) -> i32) -> [] T {
    for i: 1 .. arr.count {
        j := i;

        while j > 0 {
            if cmp(&arr.data[j - 1], &arr.data[j]) > 0 {
                tmp := arr.data[j];
                arr.data[j] = arr.data[j - 1];
                arr.data[j - 1] = tmp;

                j -= 1;
            } else {
                break;
            }
        }
    }

    return arr;
}

#doc """
    Quicksort a slice.

    `cmp` should return greater-than 0 if `left > right`.
"""
quicksort :: #match #locked {
    (arr: [] $T, cmp: ( T,  T) -> i32) => { quicksort_impl(arr, cmp, 0, arr.count - 1); return arr; },
    (arr: [] $T, cmp: (&T, &T) -> i32) => { quicksort_impl(arr, cmp, 0, arr.count - 1); return arr; },
}

#local {
    quicksort_impl :: (arr: [] $T, cmp: $PredicateFunction, lo, hi: i32) {
        if lo < 0 || hi < 0 do return;
        if lo >= hi do return;

        pivot := quicksort_partition(arr, cmp, lo, hi);
        quicksort_impl(arr, cmp, lo, pivot - 1);
        quicksort_impl(arr, cmp, pivot + 1, hi);
    }

    quicksort_partition :: #match #local {}

    #overload
    quicksort_partition :: (arr: [] $T, cmp: (T, T) -> i32, lo, hi: i32) -> i32 {
        pivot := arr[hi];
        i := lo - 1;

        for j: lo .. hi+1 {
            if cmp(arr[j], pivot) <= 0 {
                i += 1;
                tmp := arr[i];
                arr[i] = arr[j];
                arr[j] = tmp;
            }
        }

        return i;
    }

    #overload
    quicksort_partition :: (arr: [] $T, cmp: (&T, &T) -> i32, lo, hi: i32) -> i32 {
        pivot := &arr[hi];
        i := lo - 1;

        for j: lo .. hi+1 {
            if cmp(&arr[j], pivot) <= 0 {
                i += 1;
                tmp := arr[i];
                arr[i] = arr[j];
                arr[j] = tmp;
            }
        }

        return i;
    }
}

#doc """
    Shrinks a slice, removing all duplicates of elements, using `==`
    to compare elements.

    This assumes that the elements are sorted in some fashion,
    such that equal elements would be next to each other.
"""
unique :: (arr: &[] $T) {
    idx := 0;
    while i := 0; i < arr.count - 1 {
        defer i += 1;

        if idx != i {
            arr.data[idx] = arr.data[i];
        }

        if !(arr.data[i] == arr.data[i + 1]) {
            idx += 1;
        }
    }

    arr.data[idx] = arr.data[arr.count - 1];
    arr.count = idx + 1;
}


#doc """
    Reduces a slice down to a single value, using successive calls to `f`, or invokations of the `body`.

        use core {slice, println}

        arr := i32.[1, 2, 3, 4, 5];

        slice.fold(arr, 0, (it, acc) => it + acc) |> println();

        // OR

        slice.fold(arr, 0, #(it + acc)) |> println();
"""
fold :: #match #local {}

#overload
fold :: (arr: [] $T, init: $R, f: (T, R) -> R) -> R {
    val := init;
    for it: arr do val = f(it, val);
    return val;
}

#overload
fold :: macro (arr: [] $T, init: $R, body: Code) -> R {
    acc := init;
    for it: arr do acc = #unquote body;
    return acc;
}

#doc """
    Returns `true` if *every* element in the slice meets the predicate test.
"""
every :: #match #local {}

#overload
every :: macro (arr: [] $T, predicate: (T) -> bool) => #this_package.every(arr, #(predicate(it)));

#overload
every :: macro (arr: [] $T, predicate_body: Code) -> bool {
    for arr {
        if !(#unquote predicate_body) do return false;
    }
    return true;
}

#doc """
    Returns `true` if *at least one* element in the slice meets the predicate test.
"""
some :: #match #local {}

#overload
some :: macro (arr: [] $T, predicate: (T) -> bool) => #this_package.some(arr, #(predicate(it)));

#overload
some :: macro (arr: [] $T/type_is_struct, predicate_body: Code) -> bool {
    for & arr {
        if #unquote predicate_body do return true;
    }
    return false;
}

#overload
some :: macro (arr: [] $T, predicate_body: Code) -> bool {
    for arr {
        if #unquote predicate_body do return true;
    }
    return false;
}

#doc """
    Sets all elements in a slice to be `value`.
"""
fill :: (arr: [] $T, value: T) {
    for i: arr.count {
        arr[i] = value;
    }
}

#doc """
    Sets all elements in the range to be `value`.
"""
fill_range :: (arr: [] $T, r: range, value: T) {
    for i: r {
        if i >= arr.count || i < 0 do continue;
        arr[i] = value;
    }
}

#doc """
    Converts a slice to a linked list.
"""
to_list :: (arr: [] $T, allocator := context.allocator) -> List(T) {
    new_list := list.make(T, allocator);

    for &it: arr {
        list.push_end(&new_list, *it);
    }

    return new_list;
}

#doc """
    Returns the index of the first element that matches the predicate.

    Returns `-1` if no matching element is found.
"""
find :: #match #local {}

#overload
find :: (arr: [] $T, value: T) -> i32 {
    for i: arr.count {
        if value == arr.data[i] do return i;
    }

    return -1;
}

#overload
find :: macro (arr: [] $T/type_is_struct, pred: Code) -> i32 {
    for i: arr.count {
        it := &arr[i];
        if #unquote pred do return i;
    }

    return -1;
}

#overload
find :: macro (arr: [] $T, pred: Code) -> i32 {
    for i: arr.count {
        it := arr[i];
        if #unquote pred do return i;
    }

    return -1;
}

#doc """
    Returns a pointer to the first element that equals `value`, compared using `==`.

    Returns `null` if no matching element is found.
"""
find_ptr :: (arr: [] $T, value: T) -> &T {
    for &it: arr {
        if value == *it do return it;
    }

    return null;
}

first :: #match #locked {
    macro (arr: [] $T, predicate: (T) -> bool) -> &T {
        first :: first
        return first(arr, #(predicate(it)));
    },

    macro (arr: [] $T/type_is_struct, predicate_body: Code) -> &T {
        for & arr {
            if #unquote predicate_body do return it;
        }

        return null;
    },

    macro (arr: [] $T, predicate_body: Code) -> &T {
        // This is to preserve the semantics that "it" is
        // not a pointer (same as contains), when T is not a
        // structure.
        for &it_ptr: arr {
            it := *it_ptr;
            if #unquote predicate_body do return it_ptr;
        }

        return null;
    }
}

#doc """
    Returns the number of elements for which the predicate is true.
"""
count_where :: #match #local {}

#overload
count_where :: macro (arr: [] $T, predicate: (T) -> bool) => #this_package.count_where(arr, #(predicate(it)));

#overload
count_where :: macro (arr: [] $T, predicate_body: Code) -> u32 {
    count: u32 = 0;
    for arr {
        if #unquote predicate_body do count += 1;
    }
    return count;
}


#doc """
    Creates an iterator of a sliding window over the elements of the slice, with width `width`.
"""
windows :: (arr: [] $T, width: i32) -> Iterator([] T) {
    use core {iter}

    return iter.generator(
        &.{ arr=arr, width=width, pos=0 },
        ctx => {
            if ctx.pos + ctx.width < ctx.arr.count {
                defer ctx.pos += 1;
                return ctx.arr.data[ctx.pos .. ctx.pos+ctx.width], true;
            }

            return .{}, false;
        }
    );
}

#doc """
    Creates an iterator of chunks over the elements of the slice.
    Each chunk has size `width`, with the last chunk having size `arr.count % width`.
"""
chunks :: (arr: [] $T, width: i32) -> Iterator([] T) {
    use core {iter}

    return iter.generator(
        &.{ arr=arr, width=width, pos=0 },
        ctx => {
            if ctx.pos < ctx.arr.count {
                defer ctx.pos += ctx.width;

                end := core.math.min(ctx.pos+ctx.width, ctx.arr.count);
                return ctx.arr.data[ctx.pos .. end], true;
            }

            return .{}, false;
        }
    );
}


#local fold_idx_elem :: (arr: [] $T, $cmp: Code) -> (i32, T) {
    idx  := 0;
    elem := arr[0];

    for i: 1 .. arr.count {
        A := &arr[i];
        B := &elem;
        if #unquote cmp {
            idx  = i;
            elem = arr[i];
        }
    }

    return idx, elem;
}

#doc """
    Returns the largest element in the array, using `>` to compare.
"""
greatest :: macro (arr: [] $T) -> (i32, T) {
    fold_idx_elem :: fold_idx_elem
    return fold_idx_elem(arr, #(*A > *B));
}

#doc """
    Returns the smallest element in the array, using `<` to compare.
"""
least :: macro (arr: [] $T) -> (i32, T) {
    fold_idx_elem :: fold_idx_elem
    return fold_idx_elem(arr, #(*A < *B));
}

package runtime.platform

use core {*}

FileData :: #distinct i64
DirectoryData :: #distinct u64

PollDescription :: struct {
    fd: u64;
    in_events: io.PollEvent;
    out_events: io.PollEvent;
}

#foreign "onyx_runtime" {
    __poll :: (fds: [] PollDescription, timeout: i32) -> void ---
}


#local {
    #foreign "onyx_runtime" {
        __file_open_impl :: (path: str, mode: os.OpenMode, out_handle: &FileData) -> os.FileError ---

        __file_close :: (fd: FileData) -> os.FileError ---
        __file_stat   :: (path: str, stat: &os.FileStat) -> bool ---
        __file_exists :: (path: str) -> bool ---
        __file_remove :: (path: str) -> bool ---
        __file_rename :: (old_path: str, new_path: str) -> bool ---

        __file_seek  :: (handle: FileData, to: i32, whence: io.SeekFrom) -> i32 ---
        __file_tell  :: (handle: FileData) -> u32 ---
        __file_read  :: (handle: FileData, output_buffer: [] u8, bytes_read: &u64) -> io.Error ---
        __file_write :: (handle: FileData, input_buffer: [] u8, bytes_wrote: &u64) -> io.Error ---
        __file_flush :: (handle: FileData) -> io.Error ---
        __file_size  :: (handle: FileData) -> u32 ---

        __dir_open   :: (path: str, dir: &DirectoryData) -> bool ---
        __dir_close  :: (dir: DirectoryData) -> void ---
        __dir_read   :: (dir: DirectoryData, out_entry: &os.DirectoryEntry) -> bool ---
        __dir_create :: (path: str) -> bool ---
        __dir_remove :: (path: str) -> bool ---
        __dir_cwd    :: (pathbuf: [] u8) -> i32 ---
        __dir_chdir  :: (path: cstr) -> bool ---
    }
}

__file_open :: (path: str, mode := os.OpenMode.Read) -> (FileData, os.FileError) {
    handle: FileData;
    error := __file_open_impl(path, mode, &handle);

    return handle, error;
}

// Export symbols from #foreign table
__file_close   :: __file_close
__file_stat    :: __file_stat
__file_exists  :: __file_exists
__file_remove  :: __file_remove
__file_rename  :: __file_rename
__dir_open     :: __dir_open
__dir_close    :: __dir_close
__dir_read     :: __dir_read
__dir_create   :: __dir_create
__dir_remove   :: __dir_remove

__chdir :: (path: str) -> bool {
    #persist buf: [1024] u8;
    memory.copy(~~buf, path.data, math.min(1023, path.length));

    return __dir_chdir(~~ buf);
}

__getcwd :: () -> str {
    #persist buf: [1024] u8;
    length := __dir_cwd(.{ ~~buf, 1024 });
    if length < 0 {
        return "";
    }

    return buf[0 .. length];
}

__file_stream_vtable := io.Stream_Vtable.{
    seek = (use fs: &os.File, to: i32, whence: io.SeekFrom) -> io.Error {
        now := __file_seek(data, to, whence);
        return (.None) if now >= 0 else .BadFile;
    },

    tell = (use fs: &os.File) -> Result(u32, io.Error) {
        return .{ Ok = __file_tell(data) };
    },

    read = (use fs: &os.File, buffer: [] u8) -> Result(u32, io.Error) {
        bytes_read: u64;
        error := __file_read(data, buffer, &bytes_read);
        if error != .None do return .{ Err = error };
        return .{ Ok = ~~bytes_read };
    },

    read_at = (use fs: &os.File, at: u32, buffer: [] u8) -> Result(u32, io.Error) {
        __file_seek(data, at, .Start);
        bytes_read: u64;
        error := __file_read(data, buffer, &bytes_read);
        if error != .None do return .{ Err = error };
        return .{ Ok = ~~bytes_read };
    },

    read_byte = (use fs: &os.File) -> Result(u8, io.Error) {
        byte: u8;
        error := __file_read(data, ~~ cast([1] u8) &byte, null);
        if error != .None do return .{ Err = error };
        return .{ Ok = byte };
    },

    write = (use fs: &os.File, buffer: [] u8) -> Result(u32, io.Error) {
        bytes_wrote: u64;
        error := __file_write(data, buffer, &bytes_wrote);
        if error != .None do return .{ Err = error };
        return .{ Err = ~~bytes_wrote };
    },

    write_at = (use fs: &os.File, at: u32, buffer: [] u8) -> Result(u32, io.Error) {
        __file_seek(data, at, .Start);
        bytes_wrote: u64;
        error := __file_write(data, buffer, &bytes_wrote);
        if error != .None do return .{ Err = error };
        return .{ Ok = ~~bytes_wrote };
    },

    write_byte = (use fs: &os.File, byte: u8) -> io.Error {
        b := byte;
        bytes_wrote: u64;
        error := __file_write(data, .{ &b, 1 }, &bytes_wrote);
        return error;
    },

    close = (use fs: &os.File) -> io.Error {
        __file_close(data);
        return .None;
    },

    flush = (use fs: &os.File) -> io.Error {
        return __file_flush(data);
    },

    size = (use fs: &os.File) -> i32 {
        return __file_size(data);
    },

    poll = (use fs: &os.File, ev: io.PollEvent, timeout: i32) -> Result(bool, io.Error) {
        fds: [1] PollDescription;
        fds[0] = .{
            fd = ~~data,
            in_events = ev
        };

        __poll(fds, timeout);

        if fds[0].out_events == .Closed {
            return .{ Err = .EOF }; 
        }

        return .{ Ok = fds[0].out_events == ev };
    }
};


package core.sync

//
// Once is a thread-safe mechanism for executing a particular
// function only once. It is simply a flag with a mutex.
//

#doc "Represents something will only happen once."
Once :: struct {
    done: bool;
    mutex: Mutex;
}

#inject Once.exec :: #match #local {}

#doc "Run a function with no arguments once."
#overload
Once.exec :: (o: &Once, f: () -> $R) {
    scoped_mutex(&o.mutex);
    if o.done do return;

    o.done = true;
    f();
}

#doc "Run a function with one argument once."
#overload
Once.exec :: (o: &Once, ctx: $Ctx, f: (Ctx) -> $R) {
    scoped_mutex(&o.mutex);
    if o.done do return;

    o.done = true;
    f(ctx);
}

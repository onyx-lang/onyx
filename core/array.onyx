package core.array

// ---------------------------------
//           Dynamic Arrays
// ---------------------------------
make :: proc ($T: type_expr, initial_cap := 4) -> [..] T {
    arr : [..] T;
    init(^arr, initial_cap);
    return arr;
}

init :: proc (arr: ^[..] $T, initial_cap := 4) {
    arr.count = 0;
    arr.capacity = initial_cap;
    arr.data = calloc(sizeof T * arr.capacity);
}

free :: proc (arr: ^[..] $T) {
    arr.count = 0;
    arr.capacity = 0;

    cfree(arr.data);
    arr.data = null;
}

copy :: proc (arr: ^[..] $T) -> [..] T {
    new_arr : [..] T;
    init(^new_arr, arr.count);
    new_arr.count = arr.count;

    for i: 0 .. arr.count do new_arr.data[i] = arr.data[i];
    return new_arr;
}

copy_range :: proc (arr: ^[..] $T, r: range) -> [..] T {
    new_arr : [..] T;
    init(^new_arr, r.high - r.low);
    new_arr.count = r.high - r.low;

    for i: r do new_arr.data[i] = arr.data[i];
    return new_arr;
}

clear :: proc (arr: ^[..] $T) {
    arr.count = 0;
}

ensure_capacity :: proc (arr: ^[..] $T, cap: u32) -> bool {
    if arr.capacity >= cap do return true;

    while cap > arr.capacity do arr.capacity <<= 1;
    new_data := cresize(arr.data, sizeof T * arr.capacity);
    if new_data == null do return false;
    arr.data = new_data;
    return true;
}

push :: proc (arr: ^[..] $T, x: T) -> bool {
    if !ensure_capacity(arr, arr.count + 1) do return false;
    arr.data[arr.count] = x;
    arr.count += 1;
    return true;
}

insert :: proc (arr: ^[..] $T, idx: u32, x: T) -> bool {
    if !ensure_capacity(arr, arr.count + 1) do return false;

    arr.count += 1;
    while i := arr.count; i > idx {
        arr.data[i] = arr.data[i - 1];
        i -= 1;
    }

    arr.data[idx] = x;
    return true;
}

remove :: proc (arr: ^[..] $T, elem: T) {
    move := 0;

    while i := 0; i < arr.count - move {
        defer i += 1;
        
        if arr.data[i + move] == elem do move += 1;
        if move != 0 do arr.data[i] = arr.data[i + move];
    }

    arr.count -= move;
}

delete :: proc (arr: ^[..] $T, idx: u32) {
    if idx >= arr.count do return;

    for i: idx .. arr.count - 1 {
        arr.data[i] = arr.data[i + 1];
    }

    arr.count -= 1;
}

fast_delete :: proc (arr: ^[..] $T, idx: u32) {
    if idx >= arr.count do return;

    arr.data[idx] = arr.data[arr.count - 1];
    arr.count -= 1;
}

contains :: proc (arr: ^[..] $T, x: T) -> bool {
    for it: *arr do if it == x do return true;
    return false;
}

pop :: proc (arr: ^[..] $T) -> T {
    arr.count -= 1;
    return arr.data[arr.count];
}

average :: proc (arr: ^[..] $T) -> T {
    sum := cast(T) 0;
    for it: *arr do sum += it;

    return sum / cast(T) arr.count;
}

to_slice :: proc (arr: ^[..] $T) -> [] T {
    return arr.data[0 .. arr.count];
}

/*
** Simple insertion sort
**    cmp should return >0 if left > right
*/
sort :: proc (arr: ^[..] $T, cmp: proc (T, T) -> i32) {
    for i: 1 .. arr.count {
        x := arr.data[i];
        j := i - 1;

        while j >= 0 && cmp(arr.data[j], x) > 0 {
            arr.data[j + 1] = arr.data[j];
            j -= 1;
        }

        arr.data[j + 1] = x;
    }
}

fold :: proc (arr: ^[..] $T, init: $R, f: proc (T, R) -> R) -> R {
    val := init;
    for it: *arr do val = f(it, val);
    return val;
}

// CLEANUP: :slice Move this elsewhere when more slice functionality is added
fold_slice :: proc (arr: [] $T, init: $R, f: proc (T, R) -> R) -> R {
    val := init;
    for it: arr do val = f(it, val);
    return val;
}

map :: proc (arr: ^[..] $T, data: $R, f: proc (T, R) -> T) {
    for ^it: *arr do *it = f(*it, data);
}

#private_file
greatest_impl :: (arr: ^$T, count: i32) -> (i32, T) {
    greatest_idx := 0;
    greatest     := arr[0];
    
    for i: 1 .. count {
        if arr[i] > greatest {
            greatest_idx = i;
            greatest     = arr[i];
        }
    }
    
    return greatest_idx, greatest;
}

// This pattern should be expanded on to make all kinds of folding functions.
greatest :: proc {
    (arr: [..] $T) -> (i32, T) { return greatest_impl(arr.data, arr.count); },
    (arr: [] $T)   -> (i32, T) { return greatest_impl(arr.data, arr.count); },
    (arr: [$N] $T) -> (i32, T) { return greatest_impl(cast(^T) arr, N); },
}

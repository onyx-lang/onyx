package core.io

memory :: package core.memory
math   :: package core.math
array  :: package core.array

Reader :: struct {
    stream : ^Stream;

    buffer: [] u8;
    buffer_allocator: Allocator;

    start, end: u32; // The start and ending positions of the edges of the buffer "window".    

    last_byte: i32;

    error: Error;

    done : bool; // If an .EOF was reached.

    is_empty :: reader_empty;
    read_all :: read_all;
    read_byte :: read_byte;
    unread_byte :: unread_byte;
    read_bytes :: read_bytes;
    read_string :: read_string;
    read_i32 :: read_i32;
    read_i64 :: read_i64;
    read_u32 :: read_u32;
    read_u64 :: read_u64;
    read_line :: read_line;
    read_word :: read_word;
    read_until :: read_until;
    peek_byte :: peek_byte;
    advance_line :: advance_line;
    skip_whitespace :: skip_whitespace;
    skip_bytes :: skip_bytes;

    lines :: lines;
}

reader_make :: (s: ^Stream, buffer_size := 4096, allocator := context.allocator) -> Reader {
    assert(s.vtable != null, "Stream vtable was not setup correctly.");

    reader: Reader;
    reader.stream = s;
    reader.error = .None;

    memory.alloc_slice(^reader.buffer, buffer_size, allocator);
    reader.buffer_allocator = allocator;

    reader_reset(^reader);

    return reader;
}

reader_reset :: (use reader: ^Reader) {
    start, end = 0, 0;
    last_byte = #char "\0";
    done = false;
}

reader_free :: (use reader: ^Reader) {
    memory.free_slice(^buffer, buffer_allocator);
}

// You need to free the StringStream from the context.allocator when you use this. For example,
//      reader, stream := reader_from_string(s);
//      defer cfree(stream);
reader_from_string :: (s: str) -> (Reader, ^BufferStream) {
    stream_ptr := new(BufferStream);
    *stream_ptr = buffer_stream_make(s, fixed=true, write_enabled=false);

    return reader_make(stream_ptr), stream_ptr;
}

reader_get_buffered :: (use reader: ^Reader) -> i32 {
    return end - start;
}

reader_empty :: (use reader: ^Reader) -> bool {
    return done && reader_get_buffered(reader) == 0;
}

read_all :: (use reader: ^Reader, allocator := context.allocator) -> [] u8 {
    output := array.make(u8, 128, allocator=allocator);

    while !reader_empty(reader) {
        if err := reader_read_next_chunk(reader); err != .None && err != .ReadPending {
            break;
        }

        if error != .None && error != .ReadPending {
            reader_consume_error(reader);
            break;
        }

        buffered := reader_get_buffered(reader);
        if buffered > 0 {
            array.ensure_capacity(^output, output.count + buffered);
            memory.copy(output.data + output.count, buffer.data, buffered);
            output.count += buffered;
            start = end;
        }
    }

    return output;
}

read_byte :: (use reader: ^Reader) -> u8 {
    while start == end {
        if reader_empty(reader) do return 0;

        if error != .None {
            reader_consume_error(reader);
            return 0;
        }

        if err := reader_read_next_chunk(reader); err != .None {
            return 0;
        }
    }

    byte := buffer[start];
    start += 1;
    last_byte = cast(i32) byte;
    return byte;
}

unread_byte :: (use reader: ^Reader) -> Error {
    if last_byte < 0 || (start == 0 && end > 0) {
        return .InvalidUnread;
    }

    if start > 0 {
        start -= 1;
    } else {
        end = 1;
    }

    buffer[start] = cast(u8) last_byte;
    last_byte = -1;
    return .None;
}

//
// This function tries to fill the bytes buffer full of data.
// However, if the reader is empty or if the underlying stream
// returns ReadPending, this function may only fill part / none
// of the buffer. If the function exits with a .None error, the
// buffer is filled completely.
read_bytes :: (use reader: ^Reader, bytes: [] u8) -> (i32, Error) {
    n := bytes.count;
    if n == 0 {
        if reader_get_buffered(reader) > 0 do return 0, .None;
        return 0, reader_consume_error(reader);
    }

    write_index := 0;
    while n > 0 && !reader_empty(reader) {
        if reader_read_next_chunk(reader) == .ReadPending {
            return write_index, .ReadPending;
        }

        to_write := math.min(n, end);
        memory.copy(bytes.data + write_index, buffer.data, to_write);
        n -= to_write;
        write_index += to_write;
        start += to_write;
    }

    last_byte = cast(i32) bytes[bytes.count - 1];
    return bytes.count, .None;
}

read_string :: (use reader: ^Reader, bytes := 1, allocator := context.allocator) -> str {
    buf := memory.make_slice(u8, bytes, allocator);
    bytes_read, err := read_bytes(reader, buf);
    return buf[0 .. bytes_read];
};

read_i32 :: (use reader: ^Reader) -> i32 {
    n: i32 = 0;

    skip_whitespace(reader);

    is_negative := false;
    curr, err := peek_byte(reader);
    if curr == #char "-" {
        is_negative = true;
        start += 1;
    }

    curr, err = peek_byte(reader);
    while curr >= #char "0" && curr <= #char "9" {
        n *= 10;
        n += cast(u32) (curr - #char "0"); 

        start += 1;
        curr, err = peek_byte(reader);
        if err != .None || start == end do break;
    }

    if is_negative do n = -n;
    return n;
}

read_i64 :: (use reader: ^Reader) -> i64 {
    n: i64 = 0;

    skip_whitespace(reader);

    is_negative := false;
    curr, err := peek_byte(reader);
    if curr == #char "-" {
        is_negative = true;
        start += 1;
    }

    curr, err = peek_byte(reader);
    while curr >= #char "0" && curr <= #char "9" {
        n *= 10;
        n += cast(u64) (curr - #char "0"); 

        start += 1;
        curr, err = peek_byte(reader);
        if err != .None || start == end do break;
    }

    if is_negative do n = -n;
    return n;
}

read_u32 :: (use reader: ^Reader) -> u32 {
    n: u32 = 0;

    skip_whitespace(reader);

    curr, err := peek_byte(reader);
    while curr >= #char "0" && curr <= #char "9" {
        n *= 10;
        n += cast(u32) (curr - #char "0"); 

        start += 1;
        curr, err = peek_byte(reader);
        if err != .None || start == end do break;
    }

    return n;
}

read_u64 :: (use reader: ^Reader) -> u64 {
    n: u64 = 0;

    skip_whitespace(reader);

    curr, err := peek_byte(reader);
    while curr >= #char "0" && curr <= #char "9" {
        n *= 10;
        n += cast(u64) (curr - #char "0"); 

        start += 1;
        curr, err = peek_byte(reader);
        if err != .None || start == end do break;
    }

    return n;
}

read_line :: (use reader: ^Reader, consume_newline := true, allocator := context.allocator, inplace := false) -> str {
    //
    // Reading in place is special. It does not make a special allocation for the data,
    // instead just return a pointer to the internal data.
    if inplace {
        // If reading is done inplace, the start of the buffer should be the start
        // of the data known. This makes the longest possible line be buffer.count
        // in length.
        while reader_read_next_chunk(reader) == .ReadPending ---

        count := start;
        defer start = count;

        while count < end && buffer[count] != #char "\n" {
            count += 1;
        }

        if consume_newline && count < end {
            count += 1;
        }

        return buffer[start .. count];
    }

    out: [..] u8;
    array.init(^out, allocator=allocator);

    while done := false; !done {
        count := start;
        while count < end && buffer[count] != #char "\n" {
            count += 1;
        }

        if buffer[count] == #char "\n" {
            if consume_newline && count < end do count += 1;
            done = true;
        }

        if count != start {
            array.ensure_capacity(^out, out.count + (count - start));
            memory.copy(out.data + out.count, buffer.data + start, count - start);
            out.count += count - start;
            start = count;
        }

        if !done {
            while reader_read_next_chunk(reader) == .ReadPending ---
        }

        if reader_empty(reader) do done = true;
    }

    return out;
}

read_word :: (use reader: ^Reader, numeric_allowed := false, allocator := context.allocator, inplace := false) -> str {
    skip_whitespace(reader);
    while reader_read_next_chunk(reader) == .ReadPending {
        skip_whitespace(reader);
    }

    //
    // Reading in place is special. It does not make a special allocation for the data,
    // instead just return a pointer to the internal data.
    if inplace {
        // If reading is done inplace, the start of the buffer should be the start
        // of the data known. This makes the longest possible line be buffer.count
        // in length.
        while reader_read_next_chunk(reader) == .ReadPending ---

        count := start;
        defer start = count;

        while count < end {
            curr := buffer[count];
            if     (curr >= #char "a" && curr <= #char "z")
                || (curr >= #char "A" && curr <= #char "Z")
                || curr == #char "_" {
                count += 1;
            } elseif numeric_allowed && (curr >= #char "0" && curr <= #char "9") {
                count += 1;
            } else {
                break;
            }
        }

        return buffer[start .. count];
    }

    out: [..] u8;
    array.init(^out, allocator=allocator);

    while done := false; !done {
        count := start;
        while count < end {
            curr := buffer[count];
            if     (curr >= #char "a" && curr <= #char "z")
                || (curr >= #char "A" && curr <= #char "Z")
                || curr == #char "_" {
                count += 1;
            } elseif numeric_allowed && (curr >= #char "0" && curr <= #char "9") {
                count += 1;
            } else {
                done = true;
                break;
            }
        }

        if count != start {
            array.ensure_capacity(^out, out.count + (count - start));
            memory.copy(out.data + out.count, buffer.data + start, count - start);
            out.count += count - start;
            start = count;
        }

        if !done {
            while reader_read_next_chunk(reader) == .ReadPending ---
        }

        if reader_empty(reader) do done = true;
    }

    return out;
}

read_until :: (use reader: ^Reader, until: u8, skip: u32 = 0, allocator := context.allocator, consume_end := false, inplace := false) -> str {
    //
    // Reading in place is special. It does not make a special allocation for the data,
    // instead just return a pointer to the internal data.
    if inplace {
        // If reading is done inplace, the start of the buffer should be the start
        // of the data known. This makes the longest possible line be buffer.count
        // in length.
        while reader_read_next_chunk(reader) == .ReadPending ---

        count := start;
        defer start = count;

        while count < end {
            curr := buffer[count];
            if curr != until {
                count += 1;
            } else {
                if skip == 0 do break;
                else do skip -= 1;
            }
        }

        if consume_end && count < end {
            count += 1;
        }

        return buffer[start .. count];
    }

    out: [..] u8;
    array.init(^out, allocator=allocator);

    while done := false; !done {
        count := start;
        while count < end {
            curr := buffer[count];
            if curr != until {
                count += 1;
            } else {
                if skip == 0 do break;
                else do skip -= 1;
            }
        }

        if buffer[count] == until {
            if consume_end && count < end do count += 1;
            done = true;
        }

        if count != start {
            array.ensure_capacity(^out, out.count + (count - start));
            memory.copy(out.data + out.count, buffer.data + start, count - start);
            out.count += count - start;
            start = count;
        }

        if !done {
            while reader_read_next_chunk(reader) == .ReadPending ---
        }

        if reader_empty(reader) do done = true;
    }

    return out;
}

peek_byte :: (use reader: ^Reader) -> (u8, Error) {
    if reader_empty(reader) do return 0, .EOF;

    if start == end {
        if fill_err := reader_read_next_chunk(reader); fill_err != .None {
            return 0, fill_err;
        }
    }

    return buffer[start], .None;
}

advance_line :: (use reader: ^Reader) {
    if reader_empty(reader) do return;

    while true {
        if start == end {
            if fill_err := reader_read_next_chunk(reader); fill_err != .None {
                return;
            }
        }

        defer start += 1;
        if buffer[start] == #char "\n" {
            return;
        }
    }
}

skip_whitespace :: (use reader: ^Reader) {
    if reader_empty(reader) do return;

    while start < end {
        if start == end {
            if fill_err := reader_read_next_chunk(reader); fill_err != .None {
                return;
            }
        }

        switch buffer[start] {
            case #char " ", #char "\t", #char "\n", #char "\r" {
                start += 1;
            }

            case #default do return;
        }
    }
}

skip_bytes :: (use reader: ^Reader, bytes: u32) -> (skipped: i32, err: Error) {
    if bytes == 0 do return 0, .None;
    if reader_empty(reader) do return 0, .EOF;

    remaining := bytes;
    while true {
        skip := reader_get_buffered(reader);
        if skip == 0 {
            if fill_err := reader_read_next_chunk(reader); fill_err != .None {
                return 0, fill_err;
            }

            skip = reader_get_buffered(reader);
        }

        skip = math.min(skip, remaining);
        start += skip;
        remaining -= skip;

        if remaining == 0 do return bytes, .None;

        if error != .None {
            return bytes - remaining, reader_consume_error(reader);
        }
    }

    return 0, .None;
}

lines :: (r: ^Reader, inplace := false) -> Iterator(str) {
    Context :: struct {
        r: ^Reader;
        inplace: bool;
    }

    c := new(Context, allocator=context.temp_allocator);
    c.r = r;
    c.inplace = inplace;

    next :: (use c: ^Context) -> (str, bool) {
        line := r->read_line(consume_newline=true, inplace=inplace);

        if line.count > 0 do return line, true;
        else do return "", false;
    }

    return .{ c, next };
}


#local reader_consume_error :: (use reader: ^Reader) -> Error {
    defer error = .None;
    return error;
}


//
// This function serves two purposes:
//     - Shifting the remaining data in the buffer to the start.
//       This ensures the start member will be equal to 0.
//     - Filling the empty space left in the buffer after the shift.
//
// There are only two cases where the buffer will not be filled completely.
//     - The stream returns ReadPending. This is not an critical error, but
//       does mean that the reading process should stop and be tried again later.
//     - The stream returns anything other than None. This error is silently stored
//       and be fetched using reader_consume_error later.
#local reader_read_next_chunk :: (use reader: ^Reader) -> Error {
    if start > 0 {
        // This assumes that memory.copy behaves like memmove, in that the
        // buffer may overlap, but it should do the right thing.
        memory.copy(buffer.data, buffer.data + start, end - start);
        end -= start;
        start = 0;
    }

    if done do return .None;

    if end >= buffer.count {
        return .BufferFull;
    }

    err, n := stream_read(stream, buffer[end .. buffer.count]);
    
    //
    // ReadPending is not an error, just a signal that data is not ready
    // to be read, so this should try again later. This has to return None
    // if the end is equal to start because that means that the buffer is
    // completely empty.
    if err == .ReadPending {
        error = err;
        return err if end == 0 else .None;
    }

    end += n;
    if err != .None {
        if err == .EOF do done = true;

        error = err;
        return .None;
    }

    if n > 0 {
        return .None;
    }

    done = true;
    error = .NoProgress;
    return .None;
}

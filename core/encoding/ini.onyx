package core.encoding.ini
#allow_stale_code

//
// This library allows for parsing and formatting a simple 'ini' like
// file format. It is very crude, but enables a quick way to create
// a configuration file that can be mapped to a structure.
//
// The data for the ini file is stored in a 2-deep nested structure.
// The outer structure stores the sections, and each section is
// represented as another structure. For example,
//
//     Config :: struct {
//         metadata: struct {
//             name: str;
//             version: str;
//         };
//     
//         player_settings: struct {
//             speed: f32;
//             display_name: str;
//         };
//     }
//
// This corresponds to an ini file with the following data:
//
//     [metadata]
//     name=some name
//     version=0.0.1
//     
//     [player_settings]
//     speed=1.2
//     display_name=Player
//

use core.alloc
use core.conv
use core.string
use core.io

use core { aprintf }
use core.intrinsics.types { type_is_struct }
use runtime

//
// Represents if the parsing was successful or encountered an error.
IniParseResult :: enum {
    Success;
    Error;
}

//
// Represents an error that occured while parsing.
IniParseError :: struct {
    msg: str;
    line: u32;
}

//
// Parses an ini file into the variable given. Only structures
// work with this procedure, as the structure provides the section
// and value names.
parse_ini_file :: macro (r: &io.Reader, output_ptr: &$T/type_is_struct) => {
    parse_ini_file_inner :: parse_ini_file_inner

    // No need to make the actual implementation polymorphic.
    // That can work with an any, but this macro provides proper errors
    // a pointer to a structure is not provided.
    return parse_ini_file_inner(r, output_ptr);
}

#local
parse_ini_file_inner :: (r: &io.Reader, output_ptr: any) -> (IniParseResult, IniParseError) {
    info :: runtime.info

    line := 1;
    error :: macro (msg: str) {
        return .Error, .{ msg, line };
    }

    next_line :: macro () {
        r->advance_line();
        line += 1;
    }

    output: any;
    if t := info.get_type_info(output_ptr.type); t.kind != .Pointer {
        error("Expected pointer type for parameter.");
    } else {
        output.data = *cast(&rawptr) output_ptr.data;
        output.type = (cast(&info.Type_Info_Pointer) t).to;
    }

    alloc.arena.auto(dest=[](context.temp_allocator));

    active_item_ptr  := null;
    active_item_type := void;

    r->skip_whitespace();
    while !(r->is_empty()) {
        defer r->skip_whitespace();

        if b, e := r->peek_byte(); b == '[' {
            assert(r->read_byte() == '[', "expected [");
            section_name := r->read_until(']', allocator=context.temp_allocator);
            assert(r->read_byte() == ']', "expected ]");

            stripped_section_name := string.strip_whitespace(section_name);
            member := info.get_struct_member(output.type, stripped_section_name);
            if member == null {
                error(msg = aprintf("'{}' is not a valid section name.", stripped_section_name));
            }

            active_item_ptr  = cast([&] u8) output.data + member.offset;
            active_item_type = member.type;

            parse_method := info.get_struct_method(active_item_type, "parse_ini");
            if parse_method != null {
                next_line();

                f := *cast(&(rawptr, &io.Reader) -> bool) parse_method.data;
                if !f(active_item_ptr, r) {
                    error(msg = aprintf("Failed parsing."));
                }

                r->read_until('[', inplace=true);
            }

            continue;

        } elseif e != .None {
            if e == .EOF do break;
            error(msg = aprintf("Error reading file: {}", e));
        }

        defer next_line();

        field_name := r->read_until('=', allocator=context.temp_allocator);
        assert(r->read_byte() == '=', "expected =");

        field := info.get_struct_member(active_item_type, string.strip_whitespace(field_name));
        target := cast([&] u8) active_item_ptr + field.offset;
        value_string := r->read_until('\n', allocator=context.temp_allocator);
        parsed_successfully := conv.parse_any(target, field.type, value_string);

        if !parsed_successfully {
            //
            // If the type is a string, then the value can be the entire line
            // of text. No need to force the quotes.
            if field.type == str {
                *cast(&str) target = value_string
                                  |> string.strip_whitespace()
                                  |> string.copy();

            } else {
                error(aprintf("Failed to parse value of type '{}' from string '{}'.", field.type, value_string));
            }
        }

    } else {
        error("Empty file");
    }

    return .Success, .{"",0};
}


//
// Outputs a two-level structure into an ini file. The inverse of `parse_ini_file`.
write_ini_file :: macro (w: &io.Writer, output: $T/type_is_struct, leave_empty_strings := false) => {
    write_ini_file_inner :: write_ini_file_inner

    // See note above in parse_ini_file.
    return write_ini_file_inner(w, output, leave_empty_strings);
}

#local
write_ini_file_inner :: (w: &io.Writer, output: any, leave_empty_strings := false) -> bool {
    info :: runtime.info

    output_info := cast(&info.Type_Info_Struct) info.get_type_info(output.type);
    if output_info.kind != .Struct do return false;

    for& output_info.members {
        io.write_format(w, "[{}]\n", it.name);
        defer io.write(w, "\n");

        member_info := cast(&info.Type_Info_Struct) info.get_type_info(it.type);
        if member_info.kind != .Struct do continue;

        member_data := cast([&] u8) output.data + it.offset;

        if write_method := info.get_struct_method(it.type, "write_ini"); write_method != null {
            wm := *cast(&(rawptr, &io.Writer) -> bool) write_method.data;
            if !wm(member_data, w) {
                return false;
            }

            continue;
        }

        for& prop in member_info.members {
            if !leave_empty_strings && prop.type == str {
                if cast(&str, member_data + prop.offset).length == 0 {
                    continue;
                }
            }

            io.write_format_va(w, "{}={}\n", .[
                .{&prop.name, str},
                .{member_data + prop.offset, prop.type}
            ]);
        }
    }

    return true;
}

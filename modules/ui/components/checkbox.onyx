package ui
use package core

Checkbox_Theme :: struct {
    use text_theme := Text_Theme.{};
    use animation_theme := Animation_Theme.{};

    box_color        := gfx.Color4.{ 0.2, 0.2, 0.2 };
    box_border_width := 4.0f;    @InPixels
    box_size         := 20.0f;   @InPixels

    checked_color       := gfx.Color4.{ 1, 0, 0 };
    checked_hover_color := gfx.Color4.{ 1, 0.6, 0.6 };

    background_color := gfx.Color4.{ 0.05, 0.05, 0.05 };  // Background of the checkbox button.
    hover_color      := gfx.Color4.{ 0.3, 0.3, 0.3 };
    click_color      := gfx.Color4.{ 0.5, 0.5, 0.7 };
}

default_checkbox_theme := Checkbox_Theme.{};

checkbox :: (use r: Rectangle, value: ^bool, text: str, theme := ^default_checkbox_theme, site := #callsite, increment := 0) -> bool {
    result := false;

    hash := get_site_hash(site, increment);
    animation_state := map.get(^animation_states, hash);
    mx, my := get_mouse_position();

    if is_active_item(hash) {
        if mouse_state.left_button_just_up {
            if is_hot_item(hash) && Rectangle.contains(r, mx, my) {
                result = true;
                *value = !*value;
                animation_state.click_time = 1.0f;
            }

            set_active_item(0);
        }

    } elseif is_hot_item(hash) {
        if mouse_state.left_button_down {
            set_active_item(hash);
        }
    }

    if Rectangle.contains(r, mx, my) {
        set_hot_item(hash);
    }

    if is_hot_item(hash) {
        move_towards(^animation_state.hover_time, 1.0f, theme.hover_speed);
    } else {
        move_towards(^animation_state.hover_time, 0.0f, theme.hover_speed);
    }


    box_border_width := theme.box_border_width;
    box_size         := theme.box_size;
    width, height    := Rectangle.dimensions(r);

    gfx.set_texture();
    gfx.rect(
        .{ x0 + 4, y0 + (height - box_size) / 2 },   @Cleanup @ThemeConfiguration // Arbitrary padding on left
        .{ box_size, box_size },
        color=theme.box_color,
    );

    surface_color : gfx.Color4;
    if *value {
        surface_color = theme.checked_color;
        surface_color = color_lerp(animation_state.hover_time, surface_color, theme.checked_hover_color);

    } else {
        surface_color = theme.background_color;
        surface_color = color_lerp(animation_state.hover_time, surface_color, theme.hover_color);
    }

    surface_color = color_lerp(animation_state.click_time, surface_color, theme.click_color);
    
    gfx.rect(
        .{ x0 + 4 + box_border_width, y0 + (height - box_size) / 2 + box_border_width },
        .{ box_size - box_border_width * 2, box_size - box_border_width * 2 },
        surface_color
    );

    text_width, text_height := current_font->get_dimensions(text, theme.font_size);

    draw_text_raw(
        text,
        x0 + box_size + 4 * 2,   @Cleanup @ThemeConfiguration
        y0 + current_font->get_baseline(theme.font_size) + (height - text_height) / 2,
        theme.font_size, theme.text_color);

    move_towards(^animation_state.click_time, 0.0f, theme.click_decay_speed);

    if animation_state.click_time > 0 || animation_state.hover_time > 0 {
        map.put(^animation_states, hash, animation_state);
    } else {
        map.delete(^animation_states, hash);
    }

    return result;
}
package immediate_mode

use package core
use package core.intrinsics.onyx { __initialize }

Vector2 :: struct {
    x, y: f32;
}

Color4 :: struct {
    r, g, b: f32;
    a: f32 = 1;
}

Immediate_Mode :: enum {
    Triangles;
    Lines;
}

Immediate_Vertex :: struct {
    position   : Vector2;
    color      : Color4;
    texture    : Vector2;
}

Immediate_Renderer :: struct {
    active_shader : ^Shader;

    simple_shader, textured_shader : Shader;
    alpha_shader : Shader;

    // 'verticies' contains the vertex data and the maximum number of verticies
    // that can be rendered at a given time. 'vertex_count' is used to store how
    // many verticies will be rendered in the next draw call. 'vertex_count' is
    // expected to be a multiple of 3 or 2, given that triangles or lines are
    // being rendered.
    verticies : [] Immediate_Vertex;
    vertex_count : u32;

    clear_color : Color4;

    vertex_array  : gl.GLVertexArrayObject;
    vertex_buffer : gl.GLBuffer;

    // Needs to be a multiple of 6!!
    Default_Max_Verticies :: 1020;

    mode := Immediate_Mode.Triangles;

    canvas: ^Canvas = null;

    window_width, window_height: i32;

    make :: (max_verticies := Default_Max_Verticies) -> Immediate_Renderer {
        ir : Immediate_Renderer;
        __initialize(^ir);
        init(^ir, max_verticies);

        return ir;
    }

    init :: (use ir: ^Immediate_Renderer, max_verticies := Default_Max_Verticies) {
        IMMEDIATE_VERTEX_SHADER            :: #file_contents "./shaders/basic_vertex.glsl"
        IMMEDIATE_FRAGMENT_SHADER          :: #file_contents "./shaders/basic_fragment.glsl"
        IMMEDIATE_FRAGMENT_SHADER_TEXTURED :: #file_contents "./shaders/textured_fragment.glsl"
        IMMEDIATE_ALPHA_SHADER             :: #file_contents "./shaders/alpha_fragment.glsl"

        simple_shader   = Shader.make_from_source(IMMEDIATE_VERTEX_SHADER, IMMEDIATE_FRAGMENT_SHADER);
        textured_shader = Shader.make_from_source(IMMEDIATE_VERTEX_SHADER, IMMEDIATE_FRAGMENT_SHADER_TEXTURED);
        alpha_shader    = Shader.make_from_source(IMMEDIATE_VERTEX_SHADER, IMMEDIATE_ALPHA_SHADER);
        active_shader   = ^simple_shader;

        verticies = memory.make_slice(Immediate_Vertex, max_verticies);
        memory.set(verticies.data, 0, verticies.count * sizeof Immediate_Vertex);

        vertex_array = gl.createVertexArray();

        vertex_buffer = gl.createBuffer();
        gl.bindBuffer(gl.ARRAY_BUFFER, vertex_buffer);
        gl.bufferData(gl.ARRAY_BUFFER, cast(gl.GLsizei) (max_verticies * sizeof Immediate_Vertex), gl.STREAM_DRAW);
        
        ir->init_shader_params(^simple_shader);
        ir->init_shader_params(^textured_shader);
        ir->init_shader_params(^alpha_shader);

        gl.useProgram(active_shader.program);
    }

    init_shader_params :: (use ir: ^Immediate_Renderer, shader: ^Shader) {
        gl.useProgram(shader.program);

        gl.bindVertexArray(vertex_array);
        defer gl.bindVertexArray(-1);

        gl.bindBuffer(gl.ARRAY_BUFFER, vertex_buffer);

        offset_of :: (package builtin.type_info).offset_of;
        IV :: Immediate_Vertex;

        // Position
        gl.enableVertexAttribArray(0);
        gl.vertexAttribPointer(0, 2, gl.FLOAT, false, sizeof IV, offset_of(IV, "position"));

        // Color
        gl.enableVertexAttribArray(1);
        gl.vertexAttribPointer(1, 4, gl.FLOAT, false, sizeof IV, offset_of(IV, "color"));

        // Texture
        gl.enableVertexAttribArray(2);
        gl.vertexAttribPointer(2, 2, gl.FLOAT, false, sizeof IV, offset_of(IV, "texture"));

        gl.bindBuffer(gl.ARRAY_BUFFER, -1);

        // TEMPORARY
        gl.uniformMatrix4(shader.world_uniform, false,
                          f32.[ 1, 0, 0, 0,
                                0, 1, 0, 0,
                                0, 0, 1, 0,
                                0, 0, 0, 1 ]);
    }

    free :: (use ir: ^Immediate_Renderer) {
        simple_shader->free();
        textured_shader->free();
        alpha_shader->free();

        gl.deleteVertexArray(vertex_array);
        gl.deleteBuffer(vertex_buffer);
    }

    flush :: (use ir: ^Immediate_Renderer) {
        if vertex_count == 0 do return;

        gl.bindBuffer(gl.ARRAY_BUFFER, vertex_buffer);
        gl.bufferSubData(gl.ARRAY_BUFFER, 0, .{ count = vertex_count * sizeof Immediate_Vertex, data = ~~verticies.data });
        gl.bindBuffer(gl.ARRAY_BUFFER, -1);

        gl_mode : gl.GLenum;
        switch mode {
            case .Triangles do gl_mode = gl.TRIANGLES;
            case .Lines     do gl_mode = gl.LINES;
        }

        gl.useProgram(active_shader.program);
        gl.bindVertexArray(vertex_array);
        gl.drawArrays(gl_mode, 0, vertex_count);
        gl.bindVertexArray(-1);

        vertex_count = 0;
    }

    push_vertex :: #match {
        // If a color is not provided, the previous color is used.
        (use ir: ^Immediate_Renderer, position: Vector2) {
            push_vertex(ir, position, color = verticies[vertex_count - 1].color);
        },

        (use ir: ^Immediate_Renderer, position: Vector2, color: Color4, texture: Vector2 = .{0,0}) {
            if vertex_count >= verticies.count do ir->flush();

            vertex_ptr := ^verticies[vertex_count];
            defer vertex_count += 1;

            vertex_ptr.position = position;
            vertex_ptr.color = color;
            vertex_ptr.texture = texture;
        },
    }

    rect :: (use ir: ^Immediate_Renderer, position: Vector2, size: Vector2, color: Color4 = .{1,1,1}) {
        if vertex_count >= verticies.count - 6 do ir->flush();

        vertex_ptr := ^verticies[vertex_count];
        defer vertex_count += 6;

        vertex_ptr[0] = .{ .{ position.x,          position.y          }, color, .{ 0, 0 } };
        vertex_ptr[1] = .{ .{ position.x + size.x, position.y          }, color, .{ 0, 0 } };
        vertex_ptr[2] = .{ .{ position.x + size.x, position.y + size.y }, color, .{ 0, 0 } };

        vertex_ptr[3] = .{ .{ position.x,          position.y          }, color, .{ 0, 0 } };
        vertex_ptr[4] = .{ .{ position.x + size.x, position.y + size.y }, color, .{ 0, 0 } };
        vertex_ptr[5] = .{ .{ position.x,          position.y + size.y }, color, .{ 0, 0 } };
    }

    textured_rect :: (use ir: ^Immediate_Renderer, position: Vector2, size: Vector2, texture_position: Vector2, texture_size: Vector2, color: Color4 = .{1,1,1}) {
        if vertex_count >= verticies.count - 6 do ir->flush();

        vertex_ptr := ^verticies[vertex_count];
        defer vertex_count += 6;

        vertex_ptr[0] = .{ .{ position.x,          position.y          }, color, .{ texture_position.x,                   texture_position.y                  } };
        vertex_ptr[1] = .{ .{ position.x + size.x, position.y          }, color, .{ texture_position.x + texture_size.x,  texture_position.y                  } };
        vertex_ptr[2] = .{ .{ position.x + size.x, position.y + size.y }, color, .{ texture_position.x + texture_size.x,  texture_position.y + texture_size.y } };

        vertex_ptr[3] = .{ .{ position.x,          position.y          }, color, .{ texture_position.x,                   texture_position.y                  } };
        vertex_ptr[4] = .{ .{ position.x + size.x, position.y + size.y }, color, .{ texture_position.x + texture_size.x,  texture_position.y + texture_size.y } };
        vertex_ptr[5] = .{ .{ position.x,          position.y + size.y }, color, .{ texture_position.x,                   texture_position.y + texture_size.y } };
    }

    circle :: (use ir: ^Immediate_Renderer, center: Vector2, radius: f32, color := Color4.{ 1, 1, 1 }, segments := 12) {
        if vertex_count >= verticies.count - segments * 3 do ir->flush();

        assert(verticies.count >= segments * 3, "[Immediate Renderer] Too many segments to draw in a single call.");

        vertex_ptr := ^verticies[vertex_count];
        defer vertex_count += segments * 3;

        cx := center.x;
        cy := center.y;

        px := radius + cx;
        py := cy;

        ptr_offset := 0;

        for s: 1 .. segments + 1 {
            angle := math.PI * 2 * cast(f32) s / ~~segments;
            x     := math.cos(angle) * radius + cx;
            y     := math.sin(angle) * radius + cy;

            vertex_ptr[ptr_offset + 0] = .{ .{ cx, cy }, color, .{ 0, 0 } };
            vertex_ptr[ptr_offset + 1] = .{ .{ px, py }, color, .{ 0, 0 } };
            vertex_ptr[ptr_offset + 2] = .{ .{  x,  y }, color, .{ 0, 0 } };

            px = x;
            py = y;
            ptr_offset += 3;
        }
    }

    // NOTE: Calling set_texture without a parameter will disable textured rendering.
    set_texture :: (use ir: ^Immediate_Renderer, texture_id: i32 = -1) {
        if vertex_count > 0 do flush(ir);

        previous_active := active_shader;
        if texture_id >= 0 do active_shader = ^textured_shader;
        else               do active_shader = ^simple_shader;

        // Skip the GL calls if the texture didn't change.
        if previous_active == active_shader do return;

        gl.useProgram(active_shader.program);
        gl.uniform1i(active_shader.texture_uniform, math.max(texture_id, 0));
    }

    use_alpha_shader :: (use ir: ^Immediate_Renderer, texture_id: i32 = -1) {
        if active_shader != ^alpha_shader {
            if vertex_count > 0 do flush(ir);
            active_shader = ^alpha_shader;
        }

        gl.useProgram(active_shader.program);
        gl.uniform1i(active_shader.texture_uniform, math.max(texture_id, 0));
    }

    use_ortho_projection :: (use ir: ^Immediate_Renderer, left: f32, right: f32, top: f32, bottom: f32) {
        projection_matrix := f32.[
            2 / (right - left), 0, 0, 0,
            0, 2 / (top - bottom), 0, 0,
            0, 0, -2, 0,
            -(right + left) / (right - left), -(top + bottom) / (top - bottom), -1, 1
        ];

        gl.useProgram(simple_shader.program);
        gl.uniformMatrix4(simple_shader.view_uniform, false, projection_matrix);
        gl.useProgram(textured_shader.program);
        gl.uniformMatrix4(textured_shader.view_uniform, false, projection_matrix);
        gl.useProgram(alpha_shader.program);
        gl.uniformMatrix4(alpha_shader.view_uniform, false, projection_matrix);

        gl.useProgram(active_shader.program);
    }

    @Cleanup // right now, these are raw screen coordinates, with x and y being the bottom left of the screen.
    // This is backwards from what use_ortho_projections does, so it's not great to work with.
    scissor :: (use ir: ^Immediate_Renderer, x: f32, y: f32, w: f32, h: f32) {
        if !gl.isEnabled(gl.SCISSOR_TEST) {
            ir->flush();
            gl.enable(gl.SCISSOR_TEST);
        }

        gl.scissor(~~x, ~~y, ~~w, ~~h);
    }

    scissor_disable :: (use ir: ^Immediate_Renderer) {
        if gl.isEnabled(gl.SCISSOR_TEST) {
            ir->flush();
            gl.disable(gl.SCISSOR_TEST);
        }
    }

    set_mode :: (use ir: ^Immediate_Renderer, new_mode: Immediate_Mode) {
        if mode != new_mode {
            ir->flush();
            mode = new_mode;
        }
    }

    use_canvas :: (use ir: ^Immediate_Renderer, new_canvas: ^Canvas) {
        if canvas == new_canvas do return;
        ir->flush();

        canvas = new_canvas;
        if canvas != null {
            gl.bindFramebuffer(gl.FRAMEBUFFER, canvas.framebuffer);
            gl.viewport(0, 0, canvas.width, canvas.height);

            @Note // This flips the projection because generally with using framebuffers as textures,
            // the bottom left, (0, 0) in WebGL world, you want to be the top left when you render it.
            ir->use_ortho_projection(0, ~~canvas.width, ~~canvas.height, 0);

        } else {
            gl.bindFramebuffer(gl.FRAMEBUFFER, -1);
            gl.viewport(0, 0, window_width, window_height);
            ir->use_ortho_projection(0, ~~window_width, 0, ~~window_height);
        }
    }

    set_window_size :: (use ir: ^Immediate_Renderer, width: i32, height: i32) {
        window_width = width;
        window_height = height;

        // If there is no active canvas, immediately update the values.
        // Maybe this should flush the current buffer first?
        if canvas == null {
            gl.viewport(0, 0, window_width, window_height);
            ir->use_ortho_projection(0, ~~window_width, 0, ~~window_height);
        }
    }
}




// While the immediate renderer can be used on its own, below is a set of wrapping functions
// that operate on a global immediate renderer. This is probably the most common way that 
// it will be used.

#private
immediate_renderer : Immediate_Renderer;

immediate_renderer_init :: () {
    immediate_renderer = Immediate_Renderer.make();
}

immediate_renderer_free :: () {
    Immediate_Renderer.free(^immediate_renderer);
}

vertex :: #match {
    (position: Vector2)                { immediate_renderer->push_vertex(position); },
    (position: Vector2, color: Color4) { immediate_renderer->push_vertex(position, color); },
}

rect :: (position: Vector2, size: Vector2, color: Color4 = .{1,1,1}) {
    immediate_renderer->rect(position, size, color);
}

textured_rect :: (position: Vector2, size: Vector2, texture_position: Vector2, texture_size: Vector2, color: Color4 = .{1,1,1}) {
    immediate_renderer->textured_rect(position, size, texture_position, texture_size, color);
}

circle :: (center: Vector2, radius: f32, color: Color4 = .{1,1,1}, segments := 12) {
    immediate_renderer->circle(center, radius, color, segments);
}

flush :: () do immediate_renderer->flush();

set_texture :: #match {
    (texture_id: i32 = -1) { immediate_renderer->set_texture(texture_id); },
    (texture: ^Texture, texture_index := 0) {
        immediate_renderer->set_texture(texture_index);

        if texture_index >= 0 {
            gl.activeTexture(gl.TEXTURE0 + texture_index);
            gl.bindTexture(gl.TEXTURE_2D, texture.texture);

        } else {
            gl.bindTexture(gl.TEXTURE_2D, -1);
        }
    }
}

use_ortho_projection :: (left: f32, right: f32, top: f32, bottom: f32) {
    immediate_renderer->use_ortho_projection(left, right, top, bottom);
}

use_alpha_shader :: (texture_id: i32 = -1) {
    immediate_renderer->use_alpha_shader(texture_id);
}

scissor :: (x: f32, y: f32, w: f32, h: f32) {
    immediate_renderer->scissor(x, y, w, h);
}

scissor_disable :: () {
    immediate_renderer->scissor_disable();
}

set_mode :: (mode: Immediate_Mode) {
    immediate_renderer->set_mode(mode);
}

use_canvas :: (canvas: ^Canvas) {
    immediate_renderer->use_canvas(canvas);
}

set_window_size :: (width: i32, height: i32) {
    immediate_renderer->set_window_size(width, height);
}

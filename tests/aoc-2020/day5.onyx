#load "core/std"

use package core

main :: (args: [] cstr) {
    contents := #file_contents "./tests/aoc-2020/input/day5.txt";

    vals := array.make(u32);
    defer array.free(^vals);

    max_val := 0;

    while true {
        line: str;
        string.read_line(^contents, ^line);
        if line.count == 0 do break;

        val := 0;
        for ch: line {
            val *= 2;
            if ch == #char "B" || ch == #char "R" do val += 1;
        }

        max_val = math.max(max_val, val);
        array.push(^vals, val);
    }

    missing := 0;
    array.sort(^vals, cmp_asc);
    for i: 0 .. vals.count - 1 {
        if vals[i + 1] - vals[i] != 1 do missing = vals[i] + 1;
    }

    printf("Max val: %i\n", max_val);
    printf("Your seat: %i\n", missing);
}

cmp_asc :: proc (a: $T, b: T) -> i32 do return ~~(a - b);

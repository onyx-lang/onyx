

use core {*}

main :: (args: [] cstr) {
    contents := #file_contents "./input/day5.txt"

    vals := Array.make(u32)
    defer Array.free(&vals)

    max_val := 0

    while true {
        line := str.read_until(&contents, '\n')
        str.advance(&contents)
        if line.count == 0 do break

        val := 0
        for ch in line {
            val *= 2
            if ch == 'B' || ch == 'R' do val += 1
        }

        max_val = math.max(max_val, val)
        Array.push(&vals, val)
    }

    missing := 0
    Array.sort(vals, cmp_asc)
    for i in 0 .. vals.count - 1 {
        if vals[i + 1] - vals[i] != 1 do missing = vals[i] + 1
    }

    printf("Max val: {}\n", max_val)
    printf("Your seat: {}\n", missing)
}

cmp_asc :: (a: $T, b: T) -> i32 do return ~~(a - b)

#load "core/std"

use core {*}

part_1 :: (contents: &str) -> u32 {
    chars : [26] bool;
    for &ch: chars do *ch = false;

    while true {
        line := string.read_until(contents, #char "\n");
        string.advance(contents, 1);
        if line.count == 0 do break;

        for ch: line do chars[~~ch - cast(u32) #char "a"] = true;
    }

    sum := 0;
    for ch: chars do if ch do sum += 1;

    return sum;
}

part_2 :: (contents: &str) -> u32 {
    chars : [26] u32;
    for &ch: chars do *ch = 0;

    person_count := 0;
    while true {
        line := string.read_until(contents, #char "\n");
        string.advance(contents, 1);
        if line.count == 0 do break;
        
        person_count += 1;

        for ch: line do chars[~~ch - cast(u32) #char "a"] += 1;
    }

    sum := 0;
    for ch: chars do if ch == person_count do sum += 1;

    return sum;
}

main :: (args: [] cstr) {
    contents := #file_contents "./tests/aoc-2020/input/day6.txt";

    unique_sum := 0;
    while true {
        if contents.count == 0 do break;

        unique := part_1(&contents);
        unique_sum += unique;
    }

    printf("Unique sum: {}\n", unique_sum);
}

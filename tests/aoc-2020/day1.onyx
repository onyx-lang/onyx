

use core
use core.io
use core.array
use core {printf}

main :: (args: [] cstr) {
    contents := #file_contents "./input/day1.txt";
    reader, stream := io.reader_from_string(contents);
    defer cfree(stream);

    nums := array.make(u32, 128);
    defer array.free(&nums);

    while num := 1; num > 0 {
        // This sets num to be 0 if there is no number
        num = io.read_u32(&reader);
        if num != 0 do array.push(&nums, num);
    }

    for i in nums do for j in nums do for k in nums {
        if j + i + k == 2020 {
            printf("Answer: {}\n", i * j * k);

            // Break out of all of the loops
            break break break;
        }
    }
}
